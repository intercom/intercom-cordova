buildscript {
    ext {
        pushType = ''
        new XmlSlurper().parse(file('../app/src/main/res/xml/config.xml')).preference.each {
            if (it.@name.text() == 'intercom-android-push-type') {
                pushType = it.@value.text().toLowerCase()
            }
        }
        kotlin_version = '1.8.22'
    }
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
        if (pushType == 'fcm') {
            classpath 'com.google.gms:google-services:4.3.3'
        }
        // Need to add Kotlin classpath here to avoid an issue with accessing Compose from a Java only project
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
    
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://maven.google.com' }
}

dependencies {
    implementation 'io.intercom.android:intercom-sdk-base:15.11.3'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.intercom:twig:1.3.0'
    implementation 'org.jetbrains:annotations:13.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    if (pushType == 'fcm' || pushType == 'fcm-without-build-plugin') {
        implementation 'com.google.firebase:firebase-messaging:20.+'
        implementation 'io.intercom.android:intercom-sdk-fcm:15.11.3'
    }
}

// Need to add Kotlin plugin here to avoid an issue with accessing Compose from a Java only project
apply plugin: 'kotlin-android'

if (pushType == 'fcm') {
    apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
    logger.info("Applying GoogleServicesPlugin")
} else if (pushType == 'fcm-without-build-plugin') {
    logger.info("Not applying GoogleServicesPlugin from Intercom plugin, another plugin should do this")
}
