buildscript {
    ext {
        pushType = ''
        new XmlSlurper().parse(file('../app/src/main/res/xml/config.xml')).preference.each {
            if (it.@name.text() == 'intercom-android-push-type') {
                pushType = it.@value.text().toLowerCase()
            }
        }
    }
    repositories {
        google()
        jcenter()
        mavenLocal()
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        if (pushType == 'fcm') {
            classpath 'com.google.gms:google-services:4.3.3'
        }
    }
}

repositories {
    google()
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    implementation 'io.intercom.android:intercom-sdk-base:8.2.0'
    if (pushType == 'fcm' || pushType == 'fcm-without-build-plugin') {
        implementation 'com.google.firebase:firebase-messaging:20.+'
        implementation 'io.intercom.android:intercom-sdk-fcm:8.2.0'
    }
}

def googleServicesJsonPath = '../../../google-services.json'

task copyGoogleServices(type: Copy) {
    from googleServicesJsonPath
    into '.'
}

gradle.taskGraph.whenReady {
    copyGoogleServices
}

if (pushType == 'fcm') {
    apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
} else if (pushType == 'fcm-without-build-plugin') {
    logger.info("Not applying GoogleServicesPlugin from Intercom plugin, another plugin should do this")
}
