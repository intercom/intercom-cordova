buildscript {
    ext {
        pushType = ''
        new XmlSlurper().parse(file('../app/src/main/res/xml/config.xml')).preference.each {
            if (it.@name.text() == 'intercom-android-push-type') {
                pushType = it.@value.text().toLowerCase()
            }
        }
    }
    repositories {
        google()
        jcenter()
        mavenLocal()
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.+'
        if (pushType == 'fcm') {
            classpath 'com.google.gms:google-services:3.2.0'
        }
    }
}

repositories {
    google()
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    compile 'io.intercom.android:intercom-sdk-base:5.4.1'
    if (pushType == 'fcm' || pushType == 'fcm-without-build-plugin') {
        compile 'com.google.firebase:firebase-messaging:17.+'
        compile 'io.intercom.android:intercom-sdk-fcm:5.4.1'
    }
}

def googleServicesJsonPath = '../../../google-services.json'

task copyGoogleServices(type: Copy) {
    from googleServicesJsonPath
    into '.'
}

if (pushType == 'fcm') {
    tasks.copyGoogleServices.execute()
    apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
} else if (pushType == 'fcm-without-build-plugin') {
    tasks.copyGoogleServices.execute()
    logger.info("Not applying GoogleServicesPlugin from Intercom plugin, another plugin should do this")
}
