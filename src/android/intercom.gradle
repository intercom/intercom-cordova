buildscript {
    ext {
        pushType = ''
        new XmlSlurper().parse(file('../../config.xml')).preference.each {
            if (it.@name.text() == 'intercom-android-push-type') {
                pushType = it.@value.text().toLowerCase()
            }
        }
    }
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:+'
        if (pushType == 'fcm') {
            classpath 'com.google.gms:google-services:3.0.0'
        }
    }
}

repositories {
    jcenter()
}
dependencies {
    compile 'io.intercom.android:intercom-sdk-base:3.2.+'
    if (pushType == 'gcm') {
        compile 'io.intercom.android:intercom-sdk-gcm:3.2.+'
    } else if (pushType == 'fcm') {
        compile 'com.google.firebase:firebase-messaging:10.+'
        compile 'io.intercom.android:intercom-sdk-fcm:3.2.+'
    }
}

task copyGoogleServices(type: Copy) {
    from '../../google-services.json'
    into '.'
}

if (pushType == 'fcm') {
    tasks.copyGoogleServices.execute()
    apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
}
